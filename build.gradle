plugins {
    id 'org.jetbrains.intellij' version '1.12.0'
    id 'java'
}

group 'org.example'
version '0.9'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    lombokVersion = '1.18.24'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    updateSinceUntilBuild = false

    version = '2022.2'
    type = 'IU'
    plugins = ['com.intellij.database', 'uml']
}

patchPluginXml {

}

signPlugin {
    privateKey = System.getenv("PRIVATE_KEY") == null ? null : new File(System.getenv("PRIVATE_KEY")).getText('UTF-8')
    password = System.getenv("PRIVATE_KEY_PASSWORD")
    certificateChain = System.getenv("CERTIFICATE_CHAIN") == null ? null : new File(System.getenv("CERTIFICATE_CHAIN")).getText('UTF-8')
}

runPluginVerifier {
    ideVersions = [
            "DG-2021.2",
            "DG-2021.3",
            "DG-2022.1",
            "DG-2022.2.2",
            "IU-2021.2",
            "IU-2021.3",
            "IU-2022.1",
            "IU-2022.2.1",
    ]
}

publishPlugin {
    token = System.getenv("PUBLISH_TOKEN")
}

test {
    useJUnitPlatform()
}